plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.9.23'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.9.23'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.jetbrains.kotlin.plugin.jpa' version '1.9.23'
    id 'jacoco'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'io.gitlab.arturbosch.detekt' version '1.23.6'
}

group = 'ing-sist'
version = '0.0.1-SNAPSHOT'
description = 'Engine Microservice for SnippetSearcher'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/ing-sist/printscript")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
            password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
        }
    }

    // redis packages
    maven {
        url = uri("https://maven.pkg.github.com/austral-ingsis/class-redis-streams")
        credentials {
            username = findProperty("gpr.user") as String
                    ?: System.getenv("GITHUB_ACTOR")
            password = findProperty("gpr.key")  as String
                    ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

def psVersion = "8.0"
def psModules = [
        "lexer",
        "formatter",
        "token",
        "parser",
        "interpreter",
        "ast",
        "engine",
        "common",
        "analyzer"
]

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.23.6"

    psModules.each { module ->
        implementation("org.printscript:${module}:${psVersion}")
    }


    // Redis
    implementation("org.austral.ingsis:redis-streams-mvc:0.1.13")
    implementation("org.springframework.boot:spring-boot-starter-data-redis-reactive")
    implementation("io.projectreactor.kotlin:reactor-kotlin-extensions")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-reactor")
    implementation("org.jetbrains.kotlin:kotlin-reflect")
    implementation("io.projectreactor.kotlin:reactor-kotlin-extensions")
}

kotlin {
    compilerOptions {
        freeCompilerArgs.addAll '-Xjsr305=strict'
    }
}

allOpen {
    annotation 'jakarta.persistence.Entity'
    annotation 'jakarta.persistence.MappedSuperclass'
    annotation 'jakarta.persistence.Embeddable'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.10"
}

tasks.jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
    doLast {
        println "Coverage report generated: ${reports.html.outputLocation}/index.html"
    }
}

tasks.jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            limit {
                minimum = 0.80
            }
        }
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
        }
        rule {
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.60
            }
        }
    }
}

tasks.check {
    // dependsOn jacocoTestCoverageVerification
}

spotless {
    kotlin {
        ktlint()
    }
}

detekt {
    buildUponDefaultConfig = true
    allRules = false
    config = files("$rootDir/config/detekt/detekt.yml")
    reports {
        html.required = true
        xml.required = true
        txt.required = true
        sarif.required = true
    }
    ignoreFailures = false
}

tasks.register("installGitHooks", Exec) {
    commandLine "bash", "./scripts/bootstrap.sh"
}

gradle.projectsEvaluated {
    tasks.getByName("build").dependsOn("installGitHooks")
}


// fuerzo a que se use la version 3.5.6 de spring boot
configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.springframework.boot') {
            details.useVersion '3.5.6'
        }
    }
}